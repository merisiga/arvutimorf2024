Multichar_Symbols

+S ! Nimisõna
+A ! Omadussõna
+V ! Tegusõna
+P ! Asesõna
+N ! Arvsõna
+Adv ! Määrsõna
+I ! Hüüdsõna
+C ! Sidesõna
+Adp ! Kaasõna
 
+Sg
+Pl

+Nom ! Nimetav
+Gen ! Omastav
+Part ! Osastav
+Ill ! Sisseütlev
+Ine ! Seesütlev
+Ela ! Seestütlev
+All ! Alaleütlev
+Ade ! Alalütlev
+Abl ! Alaltütlev
+Trl ! Saav
+Ter ! Rajav
+Ess ! Olev
+Abe ! Ilmaütlev
+Com ! Kaasaütlev

+gi_ki

%>                ! morfeemi piir
%{NA%}            ! nõrk aste
%#                ! liitsõna piir

G1 ! g või null
B1 ! b või null
D1 ! d või null
S1 ! s või null

T2 ! t või d

T3 ! tt või t
K3 ! kk või k
P3 ! pp või p

E1 ! e või null


LEXICON Root

	maa:maa tüüp_maa ;
	abi:abi tüüp_abi ;
	elu:elu tüüp_abi ;
	isa:isa tüüp_abi ;
	suur:suur tüüp_hiir ;
	vesi:veS1 tüüp_käsi ; 
	siga: siG1a tüüp_tuba;
	käsi: käS1 tüüp_käsi ;
	tuba:tuB1a tüüp_tuba ; 
    nimi: nim tüüp_nimi ;
	
LEXICON tüüp_tuba
    +S+Sg+Nom: gi_ki ;
    	:%{NA%} sg_gen ;
    +S+Sg+Part: gi_ki ;
    	+S+Pl:%>de semkäänded ;
    +S+Pl+Part:%>sid gi_ki ;

LEXICON tüüp_käsi
	+S+Sg+Nom:%>i gi_ki ;
		:%{NA%}e sg_gen ;
	+S+Sg+Part:%{NA%}tt gi_ki ;
		+S+Pl:%{NA%}te semkäänded ;
	+S+Pl+Part:%>i gi_ki ;

LEXICON tüüp_nimi
	+S+Sg+Nom:%>i gi_ki ;
		:%>e sg_gen ;
	+S+Sg+Part:%>e gi_ki ;
		+S+Pl:%>ede semkäänded ;
	+S+Pl+Part:%>esid gi_ki ;

LEXICON tüüp_abi
	+S+Sg+Nom%#: # ;
	+S+Sg+Nom: gi_ki ;
		: sg_gen ;
	+S+Sg+Part: gi_ki ;
		+S+Pl:%>de semkäänded ;
	+S+Pl+Part:%>sid gi_ki ;
	
LEXICON tüüp_maa
	+S+Sg+Nom%#: # ;
	+S+Sg+Nom: gi_ki ;
		: sg_gen ;
	+S+Sg+Part:%>d gi_ki ;
		+S+Pl:%>de semkäänded ;
	+S+Pl+Part:%>sid gi_ki ;

LEXICON tüüp_hiir
	+S+Sg+Nom%#: # ;
	+A+Sg+Nom%#: # ;
	+S+Sg+Nom: gi_ki ;
		:%>e  sg_gen ;
	+S+Sg+Part:%>t gi_ki ;
		+S+Pl:%>te semkäänded ;
	+S+Pl+Part:%>i  gi_ki ;
	+A+Sg+Nom: gi_ki ;
	+A+Sg+Part:%>t gi_ki ;
		+A+Pl:%>te semkäänded ;
	+A+Pl+Part:%>i  gi_ki ;


LEXICON sg_gen                    ! leksikon kõigi muutuste puhul, mis nõuavad genitiivi
	+S+Sg+Gen: gi_ki ;
	       +S+Sg: semkäänded ; 
	+S+Pl+Nom:%>d gi_ki ;
	+A+Sg+Gen: gi_ki ;
	       +A+Sg: semkäänded ; 
	+A+Pl+Nom:%>d gi_ki ;


LEXICON semkäänded
	+Gen: gi_ki ;
	+Ill:%>sse gi_ki ;
	+Ine:%>s gi_ki ;
	+Ela:%>st gi_ki ;
	+All:%>le gi_ki ;
	+Ade:%>l gi_ki ;
	+Abl:%>lt gi_ki ;
	+Trl:%>ks gi_ki ;
	+Ter:%>ni gi_ki ;
	+Ess:%>na gi_ki ;
	+Abe:%>ta gi_ki ;
	+Com:%>ga gi_ki ;

LEXICON gi_ki                      ! Leksikon sufiksite gi või ki lisamisek (vaikimisi gi)
	+gi_ki:%>gi # ;
	# ;  ! Kui gi või ki puudub 