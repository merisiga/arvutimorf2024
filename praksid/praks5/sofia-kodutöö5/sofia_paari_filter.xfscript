define Hyphen [ %- ] ;
define Number ["+Sg" | "+Pl" ];
define Case10 ["+Nom" | "+Gen" | "+Par" | "+Ill" | "+Ine" | "+Ela" | "+All" | "+Ade" | "+Abl" | "+Tra" ]; 
define CaseNinaTaga ["+Ter" | "+Ess" | "+Abe" | "+Kom"] ;
define Case [ Case10 | CaseNinaTaga ] ;

# mõlemad sõnad on kas ainsuses või mitmuses, mitte segamini
define SameNumber $[["+Sg" \Number* "+Sg"] | ["+Pl" \Number* "+Pl"]] ;

# mõlemad sõnad on samas käändes
define SameCase10 [["+Nom" \Case* "+Nom"] | 
                 ["+Gen" \Case* "+Gen"] | 
                 ["+Par" \Case* "+Par"] | 
                 ["+Ill" \Case* "+Ill"] | 
                 ["+Ine" \Case* "+Ine"] | 
                 ["+Ela" \Case* "+Ela"] | 
                 ["+All" \Case* "+All"] | 
                 ["+Ade" \Case* "+Ade"] | 
                 ["+Abl" \Case* "+Abl"] | 
                 ["+Tra" \Case* "+Tra"]] ; 

# esimene on omastavas, teine ni-na-ta-ga käändes
# kilina-kolinaga
define GenNinaTaga ["+Gen" \Case* CaseNinaTaga] ; 

# sobivad käändepaarid on seega
define PairedCase [ \Case* [SameCase10 | GenNinaTaga] \Case* ];

# sõnade vahel on:
define Middle [Number Case Hyphen] ;

# paarissõnad on:
define Pairs [ [{õde} Middle {vend}] ] ;

#define WordPairs [?* Pairs ?*] ;
# paarissõnad tegelikult on natuke rohkem kui enne sai defineeritud
define WordPairs [ Pairs ?*] ;

# eemalda esimese sõna arv ja number, et paarissõna lemma näeks ilusam välja
define CleanUp [[..] <- Case   || Number _  Hyphen \[Number | Case]* Number Case ,, 
                [..] <- Number ||   _  Case Hyphen \[Number | Case]* Number Case ] ;

regex [ CleanUp .o. [WordPairs & SameNumber & PairedCase] ] ;
